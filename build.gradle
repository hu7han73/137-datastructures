plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'onethreeseven'
version '0.0.4-SNAPSHOT'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'application'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven{url 'https://dl.bintray.com/lukehb/137-common'} //common module hosted on bintray
    maven{url 'https://dl.bintray.com/lukehb/137-geo'} //geography module hosted on bintray
    maven{url 'https://dl.bintray.com/lukehb/137-jclimod'} //cli module hosted on bintray
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'

    //to make this a plugin of trajsuite
    compile (group: 'onethreeseven', name: 'trajsuitePlugin', version: '0.0.1-SNAPSHOT') {force = true}

    compile group: 'onethreeseven', name: 'geo', version: '0.0.4'
    compile group: 'onethreeseven', name: 'common', version: '0.0.4'
    compile (group: 'onethreeseven', name: 'jclimod', version: '0.0.1-SNAPSHOT') {force = true}
}

ext.moduleName = 'onethreeseven.datastructures'
mainClassName = 'onethreeseven.datastructures.Main'



compileJava {
    println classpath.asPath
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'junit',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
        ]
        classpath = files()
    }
}

test {
    inputs.property("moduleName", moduleName)
    doFirst {
        jvmArgs = [
                '--module-path', classpath.asPath,
                '--add-modules', 'ALL-MODULE-PATH',
                '--add-reads', "$moduleName=junit",
                '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
        ]
        classpath = files()
    }
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
                'Main-Class': mainClassName,
                'Automatic-Module-Name': moduleName,
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'))
    }
}

jar.dependsOn(test)

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc{
    options.addStringOption('-module-path', classpath.asPath)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from 'build/docs/javadoc'
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version version
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.bintrayUser : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayKey') ? project.bintrayKey : System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']
    publish = true //If version should be auto published after an upload
    pkg {
        repo = '137-datastructures'
        name = '137-datastructures'
        licenses = ['MIT']
        desc = 'Data structures used for building applications, particularly data-mining projects.'
        websiteUrl = 'https://github.com/lukehb/137-datastructures'
        issueTrackerUrl = 'https://github.com/lukehb/137-datastructures/issues'
        vcsUrl = 'https://github.com/lukehb/137-datastructures.git'
        labels = ['137', 'java', 'data-mining', 'data-structures']
        publicDownloadNumbers = true
        githubRepo = 'lukehb/137-datastructures'
        githubReleaseNotesFile = 'README.md'
    }
}
